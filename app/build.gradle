import com.gandan.a1xkcd.buildSrc.Libs

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.gandan.a1xkcd"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "com.gandan.a1xkcd.TestJUnitRunner"
//        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    // fix issue android studio error: Default interface methods are only supported starting with Android N (--min-api 24)
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation Libs.Coroutine.core
    implementation Libs.Coroutine.android

    implementation Libs.AndroidX.appCompat
    implementation Libs.AndroidX.constraintLayout
    implementation Libs.AndroidX.recyclerView
    implementation Libs.AndroidX.cardView
    implementation Libs.AndroidX.lifecyleExtension
    implementation Libs.AndroidX.pagingRuntime
    implementation Libs.AndroidX.swipeRefreshLayout

    implementation Libs.OkHttp.core
    implementation Libs.Retrofit.core
    implementation Libs.Retrofit.convertGson
    implementation Libs.gson

    implementation Libs.photoView
    implementation Libs.coil

    // dagger
    implementation Libs.Dagger.core
    implementation Libs.Dagger.android
    implementation Libs.Dagger.androidSupport
    kapt Libs.Dagger.compiler
    kapt Libs.Dagger.processor

    //region androidTest

    // to let androidTest generate dagger graph too
    kaptAndroidTest Libs.Dagger.compiler
    kaptAndroidTest Libs.Dagger.processor

    androidTestImplementation Libs.AndroidX.Test.runner
    androidTestImplementation Libs.AndroidX.Test.rules
    androidTestImplementation Libs.AndroidX.Test.Espresso.core
    androidTestImplementation Libs.AndroidX.Test.Espresso.contrib

    androidTestImplementation Libs.mockitoKotlin
    androidTestImplementation Libs.OkHttp.mockWebServer
    androidTestImplementation Libs.OkHttp.tls
    androidTestImplementation Libs.OkHttp.idlingResource

    androidTestUtil Libs.AndroidX.Test.orchestrator

    //endregion

    //region test

    testImplementation Libs.junit
    testImplementation Libs.mockitoKotlin
    testImplementation Libs.OkHttp.mockWebServer
    testImplementation Libs.Coroutine.test

    //endregion test

}
